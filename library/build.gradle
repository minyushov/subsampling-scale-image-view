apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
  compileSdkVersion androidCompileSdkVersion

  defaultConfig {
    minSdkVersion androidMinSdkVersion
    targetSdkVersion androidTargetSdkVersion
  }

  compileOptions {
    sourceCompatibility javaVersion
    targetCompatibility javaVersion
  }
}

dependencies {
  implementation 'androidx.annotation:annotation:1.1.0'
  implementation 'androidx.exifinterface:exifinterface:1.3.2'
}

tasks.register("sourcesJar", Jar) {
  archiveClassifier = "sources"
  from(android.sourceSets.main.java.srcDirs)
}

afterEvaluate {
  def properties = new Properties()
  def file = rootProject.file("local.properties")
  if (file.isFile()) {
    file.withInputStream { stream ->
      properties.load(stream)
    }
  }

  publishing {
    publications {
      maven(MavenPublication) {
        groupId "io.github.minyushov"
        artifactId = "scale-image-view"
        version = "3.14.0"

        from(components.findByName("release"))
        artifact(tasks.named("sourcesJar"))

        pom {
          name = artifactId
          description = "Highly configurable, easily extendable zoom view for displaying huge images without loss of detail"
          url = "https://github.com/minyushov/subsampling-scale-image-view"
          developers {
            developer {
              id = "minyushov"
              name = "Semyon Minyushov"
              email = "minyushov@gmail.com"
            }
          }
          licenses {
            license {
              name = "Apache-2.0"
              url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
          }
          scm {
            connection = "https://github.com/minyushov/subsampling-scale-image-view.git"
            developerConnection = "https://github.com/minyushov/subsampling-scale-image-view.git"
            url = "https://github.com/minyushov/subsampling-scale-image-view/tree/master"
          }
        }
      }
    }
    repositories {
      maven {
        name "sonatype"
        url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        credentials {
          username properties["SONATYPE_USER"]
          password properties["SONATYPE_PASSWORD"]
        }
      }
    }
  }

  signing {
    useInMemoryPgpKeys(
      properties["GPG_KEY_ID"],
      properties["GPG_KEY"],
      properties["GPG_PASSWORD"]
    )
    sign(publishing.publications)
  }
}